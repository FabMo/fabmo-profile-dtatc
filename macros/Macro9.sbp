'Macro 9
'Automatic Tool Change
C89 'Load all ATC Settings
SO,&SpindleCtl,0 'Make sure spindle is off.
IF &Tool = $ToolIn THEN GOTO DONE 'Skip routine if no change needed.
IF $ATC = 6 THEN GOSUB ExtendToolBar 'Raise toolbar as soon as possible to improve cycle time.
'Update Move/Jog Speeds
&CurrentXYMove = %(71)
&CurrentZMove = %(73)
&CurrentXYJog = %(76)
&CurrentZJog = %(78)
IF %(71) > &MaxMoveXY THEN GOSUB MoveXY
IF %(73) > &MaxMoveZ THEN GOSUB MoveZ
IF %(76) > &MaxJogXY THEN GOSUB JogXY
IF %(78) > &MaxJogZ THEN GOSUB JogZ

SF,0 'Set limit checking OFF
$Z_OFFSET = %(8) - ($TH + $PlateOffset)

'Execute Tool Dropoff
StartToolDrop:
	IF $ToolIn = 0 THEN GOTO ToolData
	JZ,(%(3) + (-1 *((%(3) + %(8))))) - &SensorClear - %(63)
	J2,$TX+$SX,$TY+$SY 
	J2,($TX+$LX) * &ToolBarAlongY ,($TY+$LY) * &ToolBarAlongX
	J2,$TX * &ToolBarAlongX ,$TY * &ToolBarAlongY
	&Count = 0
	GOSUB PreActions
DropTool:
	JZ,$TZ - %(8)
	J2,($TX+($LX/4)) * &ToolBarAlongY ,($TY+($LY/4)) * &ToolBarAlongX
	SO,&DrawBar,1
	J2,$TX,$TY
	&Count = 0
	IF %(58) = 0 THEN GOTO DrawBarOpenErrorDrop
FinishDrop:
	JZ,$TZ + &THClearance - %(8)
	SO,&DrawBar,0
	JZ,$TZ + 2*&THClearance - %(8)
	&Count = 0
	IF %(57) = 1 THEN GOTO ToolDropError
	&Count = 0
	IF %(58) = 1 THEN GOTO DrawBarCloseErrorDrop
	JZ,(%(3) + (-1 *((%(3) + %(8))))) - &SensorClear - %(63)
	&Count = 0
	GOSUB PostActions
	$ToolIn = 0

'Plan for Tool Pickup
GOTO ToolData

StartToolPickup:
	J2,$TX,$TY
	&Count = 0
	GOSUB PreActions
	&Count = 0
PositionPickup:
	JZ,$TZ + 2*&THClearance - %(8)
	SO,&DrawBar,1
	JZ,$TZ + &THClearance - %(8)
	IF %(58) = 0 THEN GOTO DrawBarOpenErrorPickup
	JZ,$TZ - %(8)
	SO,&DrawBar,0
	J2,($TX+($LX/4)) * &ToolBarAlongY ,($TY+($LY/4)) * &ToolBarAlongX
	&Count = 0
	IF %(57) = 1 THEN GOTO ToolPickupError
	&Count = 0
	IF %(58) = 1 THEN GOTO DrawBarCloseErrorPickup
	$ToolIn = &Tool
	VA,,,%(3) - (($TH + $PlateOffset + $Z_OFFSET) - %(8)),,,,,$TH + $PlateOffset + $Z_OFFSET
	J2,($TX + $LX) * &ToolBarAlongY ,($TY + $LY) * &ToolBarAlongX
	JZ,(%(3) + (-1 *((%(3) + %(8))))) - &SensorClear - %(63)
	J2,$TX + $SX * &ToolBarAlongX ,$TY + $SY * &ToolBarAlongY
	J2,$TX + $SX,$TY + $SY
	GOSUB PostActions
	IF $ATC = 6 THEN GOSUB RetractToolBar 

FINISH:
	VS,&CurrentXYMove,&CurrentZMove,,,&CurrentXYJog,&CurrentZJog
DONE:
	
END

'===== Tool Data Retrieval and Setup ==============================
ToolData:
IF &Tool = 0 THEN GOTO Tool0
IF &Tool = 1 THEN GOTO Tool1
IF &Tool = 2 THEN GOTO Tool2
IF &Tool = 3 THEN GOTO Tool3
IF &Tool = 4 THEN GOTO Tool4
IF &Tool = 5 THEN GOTO Tool5
IF &Tool = 6 THEN GOTO Tool6
IF &Tool = 7 THEN GOTO Tool7
IF &Tool = 8 THEN GOTO Tool8
IF &Tool = 9 THEN GOTO Tool9
IF &Tool = 10 THEN GOTO Tool10
IF &Tool = 11 THEN GOTO Tool11
IF &Tool = 12 THEN GOTO Tool12
IF &Tool = 13 THEN GOTO Tool13
IF &Tool = 14 THEN GOTO Tool14
IF &Tool = 15 THEN GOTO Tool15
IF &Tool = 16 THEN GOTO Tool16
IF &Tool = 17 THEN GOTO Tool17
IF &Tool = 18 THEN GOTO Tool18
IF &Tool = 19 THEN GOTO Tool19
IF &Tool = 20 THEN GOTO Tool20
IF &Tool = 21 THEN GOTO Tool21
IF &Tool = 22 THEN GOTO Tool22
IF &Tool = 23 THEN GOTO Tool23
IF &Tool = 24 THEN GOTO Tool24
IF &Tool = 25 THEN GOTO Tool25
IF &Tool = 26 THEN GOTO Tool26
IF &Tool = 27 THEN GOTO Tool27
IF &Tool = 28 THEN GOTO Tool28

Tool0:
$TX = $Clip0X
$TY = $Clip0Y
$TZ = $Clip0Z
$TH = $Tool0Z
$ToolIn = 0
$Z_OFFSET = %(8) - ($TH + $PlateOffset)
GOTO FINISH

Tool1:
$TX = $Clip1X
$TY = $Clip1Y
$TZ = $Clip1Z
$TH = $Tool1Z
GOTO ToolSettings

Tool2:
$TX = $Clip2X
$TY = $Clip2Y
$TZ = $Clip2Z
$TH = $Tool2Z
GOTO ToolSettings

Tool3:
$TX = $Clip3X
$TY = $Clip3Y
$TZ = $Clip3Z
$TH = $Tool3Z
GOTO ToolSettings

Tool4:
$TX = $Clip4X
$TY = $Clip4Y
$TZ = $Clip4Z
$TH = $Tool4Z
GOTO ToolSettings

Tool5:
$TX = $Clip5X
$TY = $Clip5Y
$TZ = $Clip5Z
$TH = $Tool5Z
GOTO ToolSettings

Tool6:
$TX = $Clip6X
$TY = $Clip6Y
$TZ = $Clip6Z
$TH = $Tool6Z
GOTO ToolSettings

Tool7:
$TX = $Clip7X
$TY = $Clip7Y
$TZ = $Clip7Z
$TH = $Tool7Z
GOTO ToolSettings

Tool8:
$TX = $Clip8X
$TY = $Clip8Y
$TZ = $Clip8Z
$TH = $Tool8Z
GOTO ToolSettings

Tool9:
$TX = $Clip9X
$TY = $Clip9Y
$TZ = $Clip9Z
$TH = $Tool9Z
GOTO ToolSettings

Tool10:
$TX = $Clip10X
$TY = $Clip10Y
$TZ = $Clip10Z
$TH = $Tool10Z
GOTO ToolSettings

Tool11:
$TX = $Clip11X
$TY = $Clip11Y
$TZ = $Clip11Z
$TH = $Tool11Z
GOTO ToolSettings

Tool12:
$TX = $Clip12X
$TY = $Clip12Y
$TZ = $Clip12Z
$TH = $Tool12Z
GOTO ToolSettings

Tool13:
$TX = $Clip13X
$TY = $Clip13Y
$TZ = $Clip13Z
$TH = $Tool13Z
GOTO ToolSettings

Tool14:
$TX = $Clip14X
$TY = $Clip14Y
$TZ = $Clip14Z
$TH = $Tool14Z
GOTO ToolSettings

Tool15:
$TX = $Clip15X
$TY = $Clip15Y
$TZ = $Clip15Z
$TH = $Tool15Z
GOTO ToolSettings

Tool16:
$TX = $Clip16X
$TY = $Clip16Y
$TZ = $Clip16Z
$TH = $Tool16Z
GOTO ToolSettings

Tool17:
$TX = $Clip17X
$TY = $Clip17Y
$TZ = $Clip17Z
$TH = $Tool17Z
GOTO ToolSettings

Tool18:
$TX = $Clip18X
$TY = $Clip18Y
$TZ = $Clip18Z
$TH = $Tool18Z
GOTO ToolSettings

Tool19:
$TX = $Clip19X
$TY = $Clip19Y
$TZ = $Clip19Z
$TH = $Tool19Z
GOTO ToolSettings

Tool20:
$TX = $Clip20X
$TY = $Clip20Y
$TZ = $Clip20Z
$TH = $Tool20Z
GOTO ToolSettings

Tool21:
$TX = $Clip21X
$TY = $Clip21Y
$TZ = $Clip21Z
$TH = $Tool21Z
GOTO ToolSettings

Tool22:
$TX = $Clip22X
$TY = $Clip22Y
$TZ = $Clip22Z
$TH = $Tool22Z
GOTO ToolSettings

Tool23:
$TX = $Clip23X
$TY = $Clip23Y
$TZ = $Clip23Z
$TH = $Tool23Z
GOTO ToolSettings

Tool24:
$TX = $Clip24X
$TY = $Clip24Y
$TZ = $Clip24Z
$TH = $Tool24Z
GOTO ToolSettings

Tool25:
$TX = $Clip25X
$TY = $Clip25Y
$TZ = $Clip25Z
$TH = $Tool25Z
GOTO ToolSettings

Tool26:
$TX = $Clip26X
$TY = $Clip26Y
$TZ = $Clip26Z
$TH = $Tool26Z
GOTO ToolSettings

Tool27:
$TX = $Clip27X
$TY = $Clip27Y
$TZ = $Clip27Z
$TH = $Tool27Z
GOTO ToolSettings

Tool28:
$TX = $Clip28X
$TY = $Clip28Y
$TZ = $Clip28Z
$TH = $Tool28Z
GOTO ToolSettings

ToolSettings:
GOSUB ClipOrient
GOSUB CalcX
GOSUB CalcY
$Z_OFFSET = %(8) - ($TH + $PlateOffset)
GOTO StartToolPickup

'===== Error Subroutines ==========================================
DrawBarOpenErrorDrop:
PAUSE 1
IF %(58) = 1 THEN GOTO DrawBarOpenOKDrop
&Count = &Count + 1
DrawBarOpenOKDrop:
IF &Count <= 3 THEN GOTO DropTool
PAUSE, "Problem with drawbar opening, check air supply to machine"
GOTO DropTool

ToolDropError:
&Count = &Count + 1
IF &Count <= 3 THEN GOTO DropTool
PAUSE, "Problem with tool release, check spindle and air supply to machine"
GOTO DropTool

DrawBarCloseErrorDrop:
PAUSE 1
IF %(58) = 0 THEN GOTO DrawBarCloseOKDrop
&Count = &Count + 1
DrawBarCloseOKDrop:
IF &Count <= 3 THEN GOTO DropTool
PAUSE, "Problem with drawbar closing, check air supply to machine"
GOTO FinishDrop

DrawBarOpenErrorPickup:
PAUSE 1
IF %(58) = 1 THEN GOTO DrawBarOpenOKPickup
&Count = &Count + 1
DrawBarOpenOKPickup:
IF &Count <= 3 THEN GOTO PositionPickup
PAUSE, "Problem with drawbar opening, check air supply to machine"
GOTO PositionPickup

ToolPickupError:
&Count = &Count + 1
IF &Count <= 3 THEN GOTO DropTool
PAUSE, "Problem with tool release, check spindle and air supply to machine"
GOTO DropTool

DrawBarCloseErrorPickup:
PAUSE 1
IF %(58) = 0 THEN GOTO DrawBarCloseOKPickup
&Count = &Count + 1
DrawBarCloseOKPickup:
IF &Count <= 3 THEN GOTO DropTool
PAUSE, "Problem with drawbar closing, check air supply to machine"
GOTO FinishDrop

'===== Position Calculations ======================================
CalcX:
	$SX = (-1 * &ATC_Clipsafe * (&ToolBarAlongY / &ToolBarPosition )) + ((&ToolBarAlongX / &ToolBarPosition ) * &ToolClipSpacing * 0.5 * ((&ClipAvoid + 1) / 2) )
	$LX = (&ClipAvoid * &ATC_ClipClear * (&ToolBarAlongY / &ToolBarPosition ))
RETURN

CalcY:
	$SY = (-1 * &ATC_Clipsafe * (&ToolBarAlongX / &ToolBarPosition )) + ((&ToolBarAlongY / &ToolBarPosition ) * &ToolClipSpacing * 0.5 * ((&ClipAvoid + 1) / 2) )
	$LY = (&ClipAvoid * &ATC_ClipClear * (&ToolBarAlongX / &ToolBarPosition ))
RETURN

ClipOrient:
	&ClipAvoid = 0
	IF &ToolBarPosition + &ClipOrientation = 0 THEN GOTO ClipCenter
	&ClipAvoid = 1
	GOTO ClipOrientFinish
	ClipCenter:
	&ClipAvoid = -1
	ClipOrientFinish:
RETURN

'===== Output Actions =============================================
PreActions:
	IF $ATC = 1 THEN GOSUB OpenDustFoot
	IF $ATC = 3 THEN GOSUB ExtendToolBar
	IF $ATC = 4 THEN GOSUB ExtendToolBar
RETURN

PostActions:
	IF $ATC = 1 THEN GOSUB CloseDustFoot
	IF $ATC = 3 THEN GOSUB CloseToolBar
	IF $ATC = 4 THEN GOSUB CloseToolBar
RETURN

ExtendToolBar:
	SO,&Toolbar,1
CheckExtend:
	PAUSE 0.5
	&Count = &Count + 1
	IF %(&ToolBarSensor) = 1 THEN GOTO ExtendOK
	IF &Count > 3 THEN GOTO ExtendBarError
	GOTO CheckExtend
ExtendBarError:
	PAUSE, "Problem with toolbar extending, check toolbar and air pressure."
	GOTO CheckExtend
ExtendOK:
RETURN

RetractToolBar:
	SO,&Toolbar,0
CheckRetract:
	PAUSE 0.5
	&Count = &Count + 1
	IF %(&ToolBarSensor) = 1 THEN GOTO RetractOK
	IF &Count > 3 THEN GOTO RetractBarError
	GOTO CheckRetract
RetractBarError:
	PAUSE, "Problem with toolbar retracting, check toolbar and air pressure."
	GOTO CheckRetract
RetractOK:
RETURN

OpenDustFoot:
	SO,&DustFoot,1
RETURN

CloseDustFoot:
	SO,&DustFoot,0
RETURN

'===== Parameter Modifications ====================================
MoveXY:
	VS,&MaxMoveXY
RETURN

MoveZ:
	VS,,&MaxMoveZ
RETURN

JogXY:
	VS,,,,&MaxJogXY
RETURN

JogZ:
	VS,,,,,&MaxJogZ
RETURN